let

metaDocumentation = type function (
    message1 as (type text meta [
        Documentation.FieldCaption = "Search any keyword of a custom function",
        Documentation.SampleValues = {"Search.."},
        Formatting.IsMultiLine = true,
        Formatting.IsCode = true
    ])
) as text meta [
Documentation.Name = "Search Function by Angelo Parana",
Documentation.LongDescription = "This Power Query M designed to query custom Functions on my repository"
],



CustomFunction =
    (Search as text) as table=>
        let
            myFunction = Json.Document(Binary.Decompress(Binary.FromText("BcExDsAgCADAv/gA2bt16exuHJAYJVEwgv/vXc5huG97AHBz7Ozj1ki64LStBq/0NjXhQUFIt04mIBVv4gbru0LOKhZK+QE=", BinaryEncoding.Base64), Compression.Deflate)){0}{0},
            Source = Json.Document(Web.Contents(myFunction)),
            FlatTable = Table.FromList(Source, Splitter.SplitByNothing(), {"Functions"}, null, ExtraValues.Error),
            ExpandFunctions = Table.ExpandRecordColumn(FlatTable, "Functions", {"name", "download_url"}),
            SearchFunction = Table.SelectRows(ExpandFunctions, each Text.Contains([name],Search,Comparer.OrdinalIgnoreCase)),
            GetFunction = Table.AddColumn(
                                        SearchFunction, "File", each 
                                                Text.FromBinary(Web.Contents([download_url]))
                                            ),
            return = Table.SelectColumns(GetFunction,{"name", "File"})
        in
            return
in 
Value.ReplaceType( CustomFunction, metaDocumentation )
