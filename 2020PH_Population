let
    Web = Text.FromBinary(Web.Contents("https://psa.gov.ph/content/2020-census-population-and-housing-2020-cph-population-counts-declared-official-president?fbclid=IwAR0OPKn4R-dJJUgYxm4at7qfQ5Q7HN90DK7QWz0hVckST_I8q4IcTMYjWzU")),
    ListFromHTML = Lines.FromText(Web),
    TableFromHTML = Table.FromList(ListFromHTML, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
    NeededRows = Table.SelectRows(TableFromHTML, each Text.Contains([Column1], "/system/files/phcd/2022-12/") 
                and Text.Contains([Column1], ".xlsx")
                and not(Text.Contains([Column1], "Table"))),
    Region = Table.DuplicateColumn(NeededRows, "Column1", "Column1 - Copy"),
    CleanRegion = Table.TransformColumns(Region, {
                {"Column1", each Text.BetweenDelimiters(_, " <a href=""", ".xlsx")},
                {"Column1 - Copy", each Text.BetweenDelimiters(_, ".xlsx" ,"</a>",1)}}),
    CleanRegion2 = Table.TransformColumns(CleanRegion, {{"Column1 - Copy", each Text.End(_,Text.Length(_)-2), type text}}),
    InitialTable = Table.FirstN(Table.FromRecords({[Baranggay=null]}),0),
    MergeToBeList = Table.CombineColumns(CleanRegion2,{"Column1", "Column1 - Copy"},Combiner.CombineTextByDelimiter("|", QuoteStyle.None),"Merged")[Merged],
    MainLoop = List.Accumulate(MergeToBeList,InitialTable,(input,output)=>
        let
        basepath ="https://psa.gov.ph",
        path = Text.Split(output,"|"){0},
        myRegion= Text.Split(output,"|"){1}, 
        Source = Excel.Workbook(Web.Contents(basepath & path & ".xlsx"), null, true),
        NeededTables = Table.SelectRows(Source, each not(Text.Contains([Name], "_xlnm"))),
        RemoveTopRows = Table.Skip(NeededTables,1),
        SkipHeaders = Table.TransformColumns(RemoveTopRows,{"Data", each Table.Skip(_,4)}),
        SheetList = SkipHeaders[Name],
        
        Loop = List.Accumulate(SheetList,InitialTable,(state,current)=>
            let 
            Sheet = SkipHeaders{[Item=current,Kind="Sheet"]}[Data],
            PromoteHeaders = Table.PromoteHeaders(Sheet),
            TransCols = Table.TransformColumnNames(PromoteHeaders,Text.Trim),
            RemoveCol1 = Table.SelectColumns(TransCols,{"and Barangay","Population"}),
            SkipHead = Table.Skip(RemoveCol1,5),
            RemoveNoVals = List.Transform(Table.TransformRows(SkipHead,each Record.FieldValues(_)),each List.RemoveNulls(_)),
            ConvertToTable = Table.FromList(RemoveNoVals, Splitter.SplitByNothing(), {"Baranggay"}, null, ExtraValues.Error),
            MulTransCols = Table.TransformRows(ConvertToTable, each [
                            Area = [Baranggay]{0},
                            Value = [Baranggay]{1}]),
            ListToRecord = Table.FromList(MulTransCols, Splitter.SplitByNothing(), {"Baranggay"}, null, ExtraValues.Error),
            ExpandData = Table.ExpandRecordColumn(ListToRecord, "Baranggay", {"Area", "Value"}, {"Baranggay", "Value"}),
            RemoveErrors = Table.RemoveRowsWithErrors(ExpandData, {"Value"}),
            Municipality = Table.AddColumn(RemoveErrors, "Municipality",each current)
            in   Municipality & state ),

        AddRegion = Table.AddColumn(Loop,"Region",each myRegion)

        in AddRegion & input)
in
    MainLoop
